fetch       r1,  4
addui       r0,  r0,  32  > 55   ; connect to the data memory
addui       r9,  r0,  7          ; store 7, since it is a common offset
addui.eop   r10, ch1, 0          ; wait until we receive a block location

% MOVING UPWARDS (fetched first)
addu        r12, r10, r11        ; ============================
ld          r12, 0        > 52   ;  Load and forward data item
addui       r0,  ch0, 0   > 6    ; ============================
slti.p      r0,  r11, 8          ; see if we have hit the top of the grid
nop
p?addui     r11, r11, 1
p?fetch     r1,  19              ; we have: switch to moving down
!p?fetch    r1,  9               ; we haven't: check other conditions
nop.eop

% fetched fourth
seqi.p      r0,  r11, 63         ; see if we have finished the block
nop
p?ibjmp     -30                  ; we have: end/restart

and         r13, r11, r9
seq.p       r0,  r13, r9         ; see if we have hit the right of the grid
nop
!p?subu     r11, r11, r9
!p?ibjmp    -31                  ; we haven't: keep moving up
addui       r11, r11, 8
ibjmp.eop   -24                  ; we have: switch to moving down

% MOVING DOWNWARDS (fetched second)
addu        r12, r10, r11        ; ============================
ld          r12, 0        > 52   ;  Load and forward data item
addui       r0,  ch0, 0   > 6    ; ============================
slti.p      r0,  r11, 56         ; see if we have reached the bottom of the grid
nop
!p?addui    r11, r11, 1
!p?ibjmp    -15                  ; we have: switch to moving up
fetch.eop   r1,  8               ; we haven't: check other conditions

% fetched third
and         r13, r11, r9
seq.p       r0,  r13, r0         ; see if we have reached the left of the grid
nop
!p?addu     r11, r11, r9
!p?ibjmp    -12                  ; we haven't: keep moving down
addui       r11, r11, 8
ibjmp.eop   -23                  ; we have: switch to moving up
