fetch           r1,       32
addui           r9,  r0,  28            ; store 7, since it is a common offset
addui           r10, ch1, 0             ; wait until we receive an address, and store it
ldw.eop         r10,      0   > 1       ; load the first value early

fetch           r1,       16            ; MOVING UPWARDS
addu            r12, r10, r11           ; ============================
ldw             r12,      0   > 1       ;  Load and forward data item
addui.eop       r0,  ch0, 0   > 2       ; ============================

setlti.p        r0,  r11, 32            ; see if we have hit the top of the grid
!p?fetch        r1,       20
p?addui         r11, r11, 4
p?fetch         r1,       64            ; switch to moving down
nop.eop

seteqi.p        r0,  r11, 252           ; see if we have finished
!p?fetch        r1,       16            ; check the final condition
p?fetch         r1,       108           ; end
nop.eop

and             r13, r11, r9
seteq.p         r0,  r13, r9            ; see if we have hit the right of the grid
!p?subu         r11, r11, r9
!p?fetch        r1,       -52           ; keep moving up
p?addui         r11, r11, 32
p?fetch         r1,       28            ; switch to moving down
nop.eop

fetch           r1,       16            ; MOVING DOWNWARDS
addu            r12, r10, r11           ; ============================
ldw             r12,      0   > 1       ;  Load and forward data item
addui.eop       r0,  ch0, 0   > 2       ; ============================

setlti.p        r0,  r11, 224           ; see if we have reached the bottom of the grid
p?fetch         r1,       20            ; we haven't, so check the other condition
!p?addui        r11, r11, 4
!p?fetch        r1,       -96           ; we have, so switch to moving up
nop.eop

and             r13, r11, r9
seteq.p         r0,  r13, r0            ; see if we have reached the left of the grid
!p?addu         r11, r11, r9
!p?fetch        r1,       -36           ; keep moving down
p?addui         r11, r11, 32
p?fetch         r1,       -116          ; switch to moving up
nop.eop

fetch           r1,       -160          ; end of block: restart and wait for new block address
addui.eop       r0,  ch0, 0   > 2       ; send the final value late
